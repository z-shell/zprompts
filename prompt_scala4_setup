# Created by Sebastian Gniazdowski

prompt_scala4_help () {
    cat <<EOF
This prompt is themable. You can invoke it in following way:

prompt scala4 <prompt, default is ":: "> <prompt color> <path color> <vcs info color>

You can provide only N first arguments, N=1..4.

The default invocation is: ":: " cyan cyan magenta
EOF
}

prompt_scala4_setup () {
    local prompt_left=${1:-':: '}
    local col_left=${2:-'cyan'}
    local col_path=${3:-'cyan'}
    local col_vcs=${4:-'magenta'}

    zmodload -F zsh/stat b:zstat
    zmodload zsh/sched
    autoload -Uz vcs_info

    typeset -ga _oldpsfiles
    _oldpsfiles=( )

    typeset -gA _psvar
    _psvar[col_vcs]="$col_vcs"

    local cl_left="%B%F{$col_left}"
    local cl_path="%b%F{$col_path}"
    local cl_vcs="%B%F{$col_vcs}"
    local cl_rst="%b%f"

    local _left="$cl_left$prompt_left"
    _psvar[cl_path]="$cl_path"
    _psvar[path]="${_psvar[cl_path]}%28<*<%/%<<"
    local _path='${_psvar[path]}'
    local _vcs="$cl_vcs"'${vcs_info_msg_0_}'
    PS1="$_left$cl_rst"
    PS2="$cl_left> $cl_rst"
    RPS1="$_path$_vcs$cl_rst"
    prompt_opts=(cr subst percent)

    zstyle ':vcs_info:*' enable git svn darcs bzr hg
    zstyle ':vcs_info:*' stagedstr "%F{green}●$cl_vcs"
    zstyle ':vcs_info:*' unstagedstr "%F{yellow}●$cl_vcs"
    zstyle ':vcs_info:*' check-for-changes true
    zstyle ':vcs_info:*' formats '(%s)-[%b%u%c]-'

    add-zsh-hook precmd prompt_scala4_precmd
}

_prompt_scala4_display() {
    integer lim=4

    if (( _psdispcount <= ${#_psfiles} )); then
        _psvar[path]="${_psfiles[_psdispcount]#* }"
    else
        # All displayed, cancel
        _psdispcount=lim
    fi

    if (( _psdispcount < lim )); then
        sched +2 _prompt_scala4_display
    else
        _psvar[path]="${_psvar[cl_path]}%28<*<%/%<<"
    fi

    _psdispcount+=1

    zle && zle reset-prompt
}

prompt_scala4_precmd () {
    setopt noxtrace noksharrays localoptions

    local day_info=""

    typeset -ga _psfiles
    _psfiles=( )

    local -a changed_files
    changed_files=( )

    git diff --quiet 2>/dev/null || changed_files=( ${(f)"$( git diff --name-only 2>/dev/null )"} )
    changed_files=( $^changed_files(N) )
    if [[ "${#changed_files}" -gt 0 ]]; then
        local basedir
        basedir="$(git rev-parse --show-toplevel)/"
        local -a stat_result
        stat_result=( ${(f)"$( zstat "${basedir}${^changed_files[@]}" )"} )
        stat_result=( "${(M)stat_result[@]:#mtime*}" )
        stat_result=( "${stat_result[@]/mtime[^0-9]#/}" )

        integer min_timestamp i
        min_timestamp=stat_result[1]
        for i in "${stat_result[@]}"; do
            (( i < min_timestamp )) && min_timestamp=i
        done

        integer cur_timestamp=$( date +%s )
        integer days=$(( (cur_timestamp-min_timestamp)/(24*60*60) ))
        (( days > 0 )) && day_info="$days days"

        # Construct array with each timestamp and file
        integer size=${#changed_files}
        for (( i=1; i<=size; i++ )); do
            _psfiles+=( "${stat_result[i]} ${changed_files[i]}" )
        done

        # Sort
        _psfiles=( "${(on)_psfiles[@]}" )
    fi

    if [[ -z "$day_info" ]]; then
        zstyle ':vcs_info:*' formats ' (%s)-[%b%u%c]'
    else
        zstyle ':vcs_info:*' formats ' (%s)-[%b%u%B%F{yellow}-'"$day_info"'-%c%F{'"${_psvar[col_vcs]}"'}]'
    fi

    vcs_info

    # Start oldest file display?
    # Check if array _psfiles is different from _oldpsfiles
    typeset -aU compare
    compare=( "${_psfiles[@]}" "${_oldpsfiles[@]}" )
    if [[ "${#compare}" -ne "${#_oldpsfiles}" ]]; then
        integer -g _psdispcount=1
        sched +1 _prompt_scala4_display
    fi
    _oldpsfiles=( "${_psfiles[@]}" )
}

prompt_scala4_preview () {
    local -a colors_prompt
    colors_prompt=(red yellow green blue magenta cyan)

    local cprompt
    integer i

    if (( ! $#* )); then
        for (( i = 1; i <= ${#colors_prompt}; i++ )); do
            cprompt="${colors_prompt[$i]}"
            prompt_preview_theme scala4 ":: " "$cprompt" "$cprompt"
            (( i != ${#colors_prompt} )) && print
        done
    else
        prompt_preview_theme scala4 "$@"
    fi
}

prompt_scala4_setup "$@"

# vim:ft=zsh
